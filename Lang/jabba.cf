 
-- Programs -----------------------------------------------

entrypoints     Program ;

PProgram.       Program ::= [Instr] ;

DFun.           Instr ::= "fun" Ident "(" [Arg] ")" ":" Type Block ;

-- separator nonempty       TopDecl "" ;

RefMutArg.      Arg ::= "var" Ident ":" Type;

RefConstArg.    Arg ::= "val" Ident ":" Type;

CopyMutArg.     Arg ::= "var" Ident ":" "new" Type ;

CopyConstArg.   Arg ::= "val" Ident ":" "new" Type ;

separator       Arg "," ;

-- Declarations -------------------------------------------

DItemVal.       Item ::= Ident ":" Type "=" Expr ;

DItem.          Item ::= Ident ":" Type ;

separator       Item "," ;

DVar.           Decl ::= "var" [Item] ;

DVal.           Decl ::= "val" [Item] ;

-- Instructions -------------------------------------------

IBlock.         Block ::= "{" [Instr] "}" ;

separator       Instr "" ;

IUnit.          Instr ::= ";" ;

IIncr.          Instr ::= Ident "++" ";" ;

IDecr.          Instr ::= Ident "--" ";" ;

IAss.           Instr ::= Ident "=" Expr ";" ;

IRet.           Instr ::= "return" Expr ";" ;

IRetUnit.       Instr ::= "return" ";" ;

IYield.         Instr ::= "yield" Expr ";" ;

IYieldUnit.     Instr ::= "yield" ";" ;

IBreak.         Instr ::= "break" ";" ;

ICont.          Instr ::= "continue" ";" ;

IIf.            Instr ::= "if" Expr Block ;

IIfElse.        Instr ::= "if" Expr Block "else" Block ;

IWhile.         Instr ::= "while" Expr Block ;

IWhileFin.      Instr ::= "while" Expr Block "finally" Block ;

IFor.           Instr ::= "for" Ident "=" Expr ".." Expr Block ;

IForGen.        Instr ::= "for" Ident "in" Expr Block ;

IExpr.          Instr ::= Expr ";" ;

IDecl.          Instr ::= Decl ";" ;

IBBlock.        Instr ::= Block ;

-- Types --------------------------------------------------

TInt.           Type ::= "Integer" ;

TBool.          Type ::= "Boolean" ;

TString.        Type ::= "String" ;

TGen.           Type ::= "Gen" "[" Type "]" ;

TVoid.          Type ::= "Unit" ;

-- Operators ----------------------------------------------

OPlus.          PlsOp ::= "+" ;

OMinus.         PlsOp ::= "-" ;

OMul.           MulOp ::= "*" ;

ODiv.           MulOp ::= "/" ;

OMod.           MulOp ::= "%" ;

ONot.           NotOp ::= "!" ;

ONeg.           NegOp ::= "-" ;

OAnd.           AndOp ::= "&&" ;

OOr.            OrOp  ::= "||" ;

-- Equality -----------------------------------------------

REq.            RelOp ::= "==" ;

RNeq.           RelOp ::= "!=" ;

-- Relations ----------------------------------------------

RLt.            RelOp ::= "<" ;

RGt.            RelOp ::= ">" ;

RLeq.           RelOp ::= "<=" ;

RGeq.           RelOp ::= ">=" ;

-- Expressions --------------------------------------------

EVarName.       Expr6 ::= Ident ;

EIntLit.        Expr6 ::= Integer ;

EBoolLitTrue.   Expr6 ::= "true" ;

EBoolLitFalse.  Expr6 ::= "false" ;

ERun.           Expr6 ::= Ident "(" [Expr] ")" ;

EStringLit.     Expr6 ::= String ;

ENeg.           Expr5 ::= NegOp Expr6 ;

ENot.           Expr5 ::= NotOp Expr6 ;

EMul.           Expr4 ::= Expr4 MulOp Expr5 ;

ESum.           Expr3 ::= Expr3 PlsOp Expr4 ;

ERel.           Expr2 ::= Expr2 RelOp Expr3 ;

EBAnd.          Expr1 ::= Expr2 AndOp Expr1 ;

EBOr.           Expr ::= Expr1 OrOp Expr ;

ETer.           Expr ::= Expr1 "?" Expr1 ":" Expr ;

coercions       Expr 6 ;

separator       Expr "," ;

-- Comments -----------------------------------------------

comment         "#" ;

comment         "//" ;

comment         "/*" "*/" ;

